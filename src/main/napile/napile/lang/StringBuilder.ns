package napile.lang

import napile.io.Console

/**
 * Created with IntelliJ IDEA.
 * User: VISTALL
 * Date: 05.10.12
 * Time: 17:33
 * To change this template use File | Settings | File Templates.
 */
class StringBuilder
{
	local var array : Array<Char> = Array<Char>(10)
	local var count : Int = 0

	this()

	meth add(value : Any)
	{
		plusAssign(value)
	}

	meth plusAssign(value : Any)
	{
		var str = value.toString()

		var arrayOfString = str.array;

		var newCount = arrayOfString.length + count
		if(newCount > array.length)
			expandCapacity(newCount)

		var i = 0;
		while (i < arrayOfString.length)
			array[count ++] = arrayOfString[i++];

		count = newCount;
	}

	meth expandCapacity(minimumCapacity : Int)
	{
		var newCapacity = (array.length + 1) * 2;
		if (newCapacity < 0)
			newCapacity = 0x7fffffff;
		else if (minimumCapacity > newCapacity)
			newCapacity = minimumCapacity;

		array = ArrayUtil.copyOf(array, newCapacity);
	}

	override meth toString() : String
	{
		return String(array, 0, count);
	}
}