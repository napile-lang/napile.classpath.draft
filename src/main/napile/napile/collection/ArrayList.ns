package napile.collection

/**
 * @author VISTALL
 */
class ArrayList<E> : List<E>
{
	local var data : Array<E>
	local var size : Int = 0

	this(predefinedSize : Int = 10) : List<E>()
	{
		data = Array<E>(predefinedSize)
	}

	override meth add(e : E) : Bool
	{
		ensureCapacity(size + 1);
		data[size++] = e;
		return true;
	}

	override meth get(index : Int) : E = data[index]

	local meth ensureCapacity(minCapacity : Int)
	{
		var oldCapacity = data.length;
		if (minCapacity > oldCapacity)
		{
			var newCapacity = (oldCapacity * 3) / 2 + 1;
			if (newCapacity < minCapacity)
				newCapacity = minCapacity;

			data = ArrayUtil.copyOf(data, newCapacity);
		}
	}

	override meth iterator() : Iterator<E> = ListIterator<E>(this)

	override meth size() = size
}